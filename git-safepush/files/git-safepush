#!/usr/bin/env perl

use 5.12.0;

use Git::Wrapper;
use File::chdir;
use IO::All;
use List::MoreUtils qw/ any /;

my $remote = shift @ARGV or die "usage: $0 <remote>";

my ( $local_branch, $remote_branch ) = split ':', shift @ARGV;

pop @CWD until any { /\.git$/ } glob "$CWD/.g*" or not @CWD;

die "not in a git repository\n" if $CWD eq '/';

my $git = Git::Wrapper->new($CWD);

$local_branch ||= ( ($local_branch) = grep { s/\* // } $git->branch )[0];
$remote_branch ||= $local_branch;

my $filename;

for ( $git->diff( join( '/', $remote, $remote_branch ), $local_branch ) ) {

    $filename = $1 when /^diff --git .* b\/(.*?)$/;

    die "'DO NO PUSH' seen in file '$filename', aborting push\n" when
      /^\+.*#\s*DO NOT PUSH/;
}

$git->push( $remote, "$local_branch:$remote_branch" );
